plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

apply from: "$rootDir/dependencies.gradle"

android {
    namespace "com.schaefer.livenesscamerax"
    resourcePrefix 'liveness_camerax'

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildToolsVersion

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    kotlinOptions {
        jvmTarget = versions.jvmTarget
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    implementation project(path: ":livenesscamerax:core")
    implementation project(path: ":livenesscamerax:domain")
    implementation project(path: ":livenesscamerax:camera")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

    //Android
    implementation "androidx.core:core-ktx:$versions.ktx"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    implementation "androidx.activity:activity-ktx:$versions.activity"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.cardview:cardview:$versions.cardview"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"

    //Google Services
    implementation "com.google.android.gms:play-services-mlkit-face-detection:$versions.faceDetection"
    implementation "com.google.mlkit:vision-common:$versions.vision"

    //CameraX
    implementation "androidx.camera:camera-camera2:$versions.cameraX"
    implementation "androidx.camera:camera-lifecycle:$versions.cameraX"
    implementation "androidx.camera:camera-view:$versions.cameraXOthers"

    //Square
    implementation "com.jakewharton.timber:timber:$versions.timber"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.schaefer.livenesscamerax"
                artifactId = "livenesscamerax"
                version = '0.0.2'
            }
        }
    }
}